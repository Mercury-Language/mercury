#-----------------------------------------------------------------------------#
# Copyright (C) 1996-2002 The University of Melbourne. 
# This file may only be copied under the terms of the GNU General
# Public Licence - see the file COPYING in the Mercury distribution.
#-----------------------------------------------------------------------------#
#
# Define a macro MERCURY_MSG, similar to AC_MSG_RESULT, for displaying output.
#
dnl MERCURY_MSG(MESSAGE)
define(MERCURY_MSG,
[echo $1 1>&AC_FD_MSG])
#-----------------------------------------------------------------------------#

AC_INIT(./build_vars)
AC_PREFIX_DEFAULT(/usr/local/mercury-<VERSION>)
AC_CANONICAL_HOST
if test "$prefix" = "NONE"; then
	PREFIX="$ac_default_prefix"
else
	PREFIX="$prefix"
fi
LIBDIR="$PREFIX/lib/mercury"
WINDOWS_LIBDIR="`cygpath -w $LIBDIR 2>/dev/null`"
NONSHARED_LIB_DIR=${MERCURY_NONSHARED_LIB_DIR=$PREFIX/lib/nonshared}
AC_SUBST(PREFIX)
AC_SUBST(LIBDIR)
AC_SUBST(WINDOWS_LIBDIR)
AC_SUBST(NONSHARED_LIB_DIR)
DEFAULT_MERCURY_DEBUGGER_INIT_DIR=$LIBDIR/mdb
AC_SUBST(DEFAULT_MERCURY_DEBUGGER_INIT_DIR)

#-----------------------------------------------------------------------------#
#
# Source the build_vars script to get the variables whoes bindings
# were determined at the time when the binary distribution was built.

. ./build_vars

#-----------------------------------------------------------------------------#
#
# Pass on the inherited variables

AC_SUBST(FULLARCH)
AC_SUBST(LIBGRADES)
AC_SUBST(DEFAULT_GRADE)
AC_SUBST(NUM_REAL_R_REGS)
AC_SUBST(NUM_REAL_R_TEMPS)
AC_SUBST(HAVE_BOXED_FLOATS)
AC_SUBST(BYTES_PER_WORD)
AC_SUBST(BITS_PER_WORD)
AC_SUBST(LOW_TAG_BITS)
AC_SUBST(CFLAGS_FOR_REGS)
AC_SUBST(CFLAGS_FOR_GOTOS)
AC_SUBST(CFLAGS_FOR_THREADS)
AC_SUBST(LINK_SHARED_OBJ)
AC_SUBST(LINK_SHARED_OBJ_SH)
AC_SUBST(CFLAGS_FOR_PIC)
AC_SUBST(EXE_RPATH_OPT)
AC_SUBST(EXE_RPATH_SEP)
AC_SUBST(SHLIB_RPATH_OPT)
AC_SUBST(SHLIB_RPATH_SEP)
AC_SUBST(EXT_FOR_EXE)
AC_SUBST(EXT_FOR_PIC_OBJECTS)
AC_SUBST(EXT_FOR_SHARED_LIB)
AC_SUBST(SHARED_LIBS)
AC_SUBST(HAVE_DELAY_SLOT)
AC_SUBST(ALLOW_UNDEFINED)
AC_SUBST(ERROR_UNDEFINED)
AC_SUBST(DL_LIBRARY)
AC_SUBST(SOCKET_LIBRARY)
AC_SUBST(NSL_LIBRARY)

AC_SUBST(OBJFILE_OPT)
AC_SUBST(AR)
AC_SUBST(ARFLAGS)
AC_SUBST(AR_LIBFILE_OPT)
AC_SUBST(BOEHMGC_MAKEFILE)
AC_SUBST(EXE_SUFFIX)
AC_SUBST(OBJ_SUFFIX)
AC_SUBST(LIB_SUFFIX)
AC_SUBST(LIB_PREFIX)
AC_SUBST(LIB_LIBPATH)
AC_SUBST(LINK_LIB)
AC_SUBST(LINK_OPT_SEP)
AC_SUBST(CYGPATH)
AC_SUBST(MATH_LIB)
AC_SUBST(SHARED_LIBS_SH)
AC_SUBST(CGIDIR)
AC_SUBST(ENABLE_DEEP_PROFILER)

#-----------------------------------------------------------------------------#
AC_PROG_RANLIB
#-----------------------------------------------------------------------------#
GNU_MAKE=
echo looking for GNU Make...
AC_PROGRAMS_CHECK(GNU_MAKE,gmake make)
if test "$GNU_MAKE" != ""; then
	case "`$GNU_MAKE -v bogus 2>&1`" in
		"GNU Make"*)
			;;
		*)
			GNU_MAKE=
	esac
fi
if test "$GNU_MAKE" = ""; then
	AC_MSG_ERROR(cannot find GNU Make)
else
	echo "found GNU Make: $GNU_MAKE"
fi
AC_SUBST(GNU_MAKE)
#-----------------------------------------------------------------------------#
echo "looking for a way to create named pipes..."

save_PATH="$PATH"
PATH="$PATH:/etc:/usr/etc:/sbin"

MKFIFO=
AC_PATH_PROG(MKFIFO,mkfifo)
if test "$MKFIFO" != ""; then
	# check that it really works
	tmp=/tmp/fifo$$
	trap "rm -f $tmp" 1 2 3 13 15
	if $MKFIFO $tmp && test -p $tmp; then
		true
	else
		MKFIFO=""
	fi
	rm -f $tmp
fi
MKNOD=
if test "$MKFIFO" = ""; then
	AC_PATH_PROG(MKNOD,mknod)
	if test "$MKNOD" != ""; then
		# check that it really works
		tmp=/tmp/fifo$$
		trap "rm -f $tmp" 1 2 3 13 15
		if $MKNOD $tmp p && test -p $tmp; then
			MKFIFO=mkfifo_using_mknod
		else
			MKNOD=
		fi
	fi
fi
if test "$MKFIFO" = ""; then
	AC_MSG_WARN(cannot find a working mkfifo or mknod)
	MKFIFO=none
fi
AC_SUBST(MKFIFO)
test "$MKNOD" = "" && MKNOD=mknod
AC_SUBST(MKNOD)

PATH="$save_PATH"
#-----------------------------------------------------------------------------#
MERCURY_MSG("looking for a way to create temporary files...")

AC_PATH_PROG(MKTEMP,mktemp)
if test "$MKTEMP" != ""; then
	# check that it really works
	TMPFILE="`mktemp /tmp/configure.XXXXXX < /dev/null`"
	case "$TMPFILE" in 
		/tmp/configure.*) rm -f $TMPFILE ;;
		*) MKTEMP="" ;;
	esac
fi
if test "$MKTEMP" = ""; then
	AC_MSG_WARN(cannot find a working \`mktemp', using \`mkdir || exit'')
	MKTEMP=""
fi
AC_SUBST(MKTEMP)
#-----------------------------------------------------------------------------#
AC_PATH_PROG(AS,as)
AC_MSG_CHECKING(whether the assembler does full preprocessing)

AC_CACHE_VAL(mercury_cv_asm_does_full_preprocessing, [
if test "$AS" != ""; then
	# check that it really works
	cat > conftest.s << EOF

# define foo(x) \
	/* foo */

	foo(x)
EOF
	if 
		echo $AS conftest.s >&AC_FD_CC 2>&1 &&
		$AS conftest.s </dev/null >&AC_FD_CC 2>&1 
	then
		mercury_cv_asm_does_full_preprocessing="yes"
	else
		AS="gcc -c -x assembler-with-cpp"
		mercury_cv_asm_does_full_preprocessing="no"
		AC_MSG_RESULT(no)	
	fi
	rm -f conftest*
else
	AS="gcc -c -x assembler-with-cpp"
fi
])
AC_MSG_RESULT($mercury_cv_asm_does_full_preprocessing)	
AC_SUBST(AS)
#-----------------------------------------------------------------------------#
AC_PROG_CC
AC_SUBST(CC)
#-----------------------------------------------------------------------------#
# Microsoft.NET configuration (the bindist may be installed on a machine
# that supports .NET even if it was built on a machine that doesn't).
# 
MERCURY_CHECK_DOTNET

# The following allows us to share some subroutines between the
# `ml' and `mgnuc' scripts.

top=`pwd`
INIT_GRADE_OPTIONS=$top/scripts/init_grade_options.sh-subr
PARSE_GRADE_OPTIONS=$top/scripts/parse_grade_options.sh-subr
FINAL_GRADE_OPTIONS=$top/scripts/final_grade_options.sh-subr
PARSE_ML_OPTIONS=$top/scripts/parse_ml_options.sh-subr
CANONICAL_GRADE=$top/scripts/canonical_grade.sh-subr
AC_SUBST_FILE(INIT_GRADE_OPTIONS)
AC_SUBST_FILE(PARSE_GRADE_OPTIONS)
AC_SUBST_FILE(FINAL_GRADE_OPTIONS)
AC_SUBST_FILE(PARSE_ML_OPTIONS)
AC_SUBST_FILE(CANONICAL_GRADE)

#-----------------------------------------------------------------------------#
MERCURY_CHECK_READLINE
#-----------------------------------------------------------------------------#
AC_OUTPUT(Makefile scripts/mmc scripts/mercury.bat scripts/mprof scripts/mdprof
scripts/mercury_update_interface scripts/mgnuc scripts/ml
scripts/mmake scripts/c2init scripts/sicstus_conv
scripts/mkfifo_using_mknod scripts/Mmake.vars
scripts/mdb scripts/mdbrc
,
# conftest.junk is used to avoid a warning if there
# are no files in the list passed to chmod
touch conftest.junk
chmod a-w $CONFIG_FILES conftest.junk
chmod +x `echo $CONFIG_FILES | sed -e 's/[^ ]*Makefile//' ` conftest.junk
rm -f conftest.junk
)

#-----------------------------------------------------------------------------#
