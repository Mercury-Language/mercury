#!/usr/bin/awk -f
# vim: ft=awk ts=4 sw=4 et
#
# This script summarizes the contents of the MAKE_DEPS_CACHE_STATS
# files generated by the conditionally-enabled statistics-gathering code
# at the end of compiler/make.deps_cache.m.
#

NF == 3 {
        cache = $1;
        cur_lookups = $2;
        cur_hits = $3;
        cur_misses = cur_lookups - cur_hits;

        caches[cache] += 1;
        lookups[cache] += cur_lookups;
        hits[cache] += cur_hits;
        misses[cache] += cur_misses;

        overall_lookups += cur_lookups;
        overall_hits += cur_hits;
        overall_misses += cur_misses;
        # printf "EXEC %s %d\n", cache, caches[cache];
    }
END {
        overall_hits = overall_lookups - overall_misses;
        overall_hit_rate = (100 * overall_hits) / overall_lookups;
        printf "number of lookups:      %12d\n", overall_lookups;
        printf "number of hits:         %12d\n", overall_hits;
        printf "number of misses:       %12d\n", overall_misses;
        printf "hit %:                        %6.2f\n\n", overall_hit_rate;

        printf "----------------------------------------------------------\n\n";

        printf "%-36s %10s %10s %6s\n\n",
            "cache", "#exec", "#lookup", "hit%";
        for (cache in caches) {
            hit_rate[cache] = (100 * hits[cache]) / lookups[cache];
            printf "%-36s %10d %10d %6.2f\n",
                cache, caches[cache], lookups[cache], hit_rate[cache];
        }
    }
